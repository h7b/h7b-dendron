---
id: 9cQxkfHauxrDVrO1Arbsz
title: Last Non Empty Column
desc: ''
updated: 1645392096986
created: 1644283869190
---
# Find the Last Non-Empty Column or Row in Google Sheets

## Find the Last Non-Empty Column in a Row in Google Sheets
ref: [infoinspired](https://infoinspired.com/google-docs/spreadsheet/last-non-empty-column-in-a-row-in-google-sheets/)

Situation: I want to find the last used column number of the row#3
![situation-example](https://infoinspired.com/wp-content/uploads/2019/10/Last-Used-Column.gif){max-width: 300px, display: block, margin: 0 auto}

How to do: use [MATCH function](https://infoinspired.com/google-docs/spreadsheet/match-function-in-google-sheets/)
```javascript
=ArrayFormula(MATCH(2,1/(A3:3<>"")))
```

Explain:
1. the key element is the array generated by `1/(A3:3<>"")`
 if we put the formula in cell `A4`
  ```javascript
  =ArrayFormula(1/(A3:3<>""))
  ```
  the output will be
![generated-array](https://infoinspired.com/wp-content/uploads/2019/10/Last-Value-Logic-Sheets.jpg){max-width: 300px, display: block, margin: 0 auto}
  Meaning of this formula: returns the number 1 wherever the cells in row#3 have values, else `#DIV/0!` error
2. next is the MATCH function
  ```javascript
  match(2,1/(A3:3<>""))
  ```
  The function would return the largest value less than or equal to the `search_key` (number 2) from the A-Z sorted `range` (the array generated by `ArrayFormula(1/(A3:3<>""))`)  
  Since the `search_key` number 2 is not available in the generated array, the MATCH function will then search for the largest number less than 2, which is 1.  
  Since there are multiple cells containing the number 1, the formula would return the relative position of the last cell containing the number 1

> Note: This formula can be applied to generate automatically column number
```javascript
=ArrayFormula(SEQUENCE(1,MATCH(2,1/(A2:2<>""))))
```
![example](https://ik.imagekit.io/casa/h7b-dendron/Screenshot_2022-02-10_154826_e09IEdtwH.jpg?ik-sdk-version=javascript-1.4.3&updatedAt=1644504538719){max-width: 300px, display: block, margin: 0 auto}

## Find the Last Non-Empty Row in Google Sheets
ref: [Learn Google Spreadsheets](https://youtu.be/nC0h6UR4E1k?t=782)

This formula return the ordinal number of last non-blank row, based on cells' value in column B
```javascript
=max(row(B:B)*(B:B<>"")
```

Explain:
- `ROW(B:B)` returns a column data of all row numbers of column B
- `B:B<>""` returns a column of `true/false` as boolean or `1/0` as number, from the logical comparison to determine whether each row in column is blank or not
- the output of multiplying these 2 arrays is a 1-column array with a `0` for each blank row
- then apply `MAX` function to this last 1-column array to return the highest number, which is equivalent to the last row which is non blank, based on value of cells in column B

> Note: This can be applied to generate automatically in column A, the row number of non-blank lines based on contents of column B.
```javascript
=ArrayFormula(IF(row(B2:B)>max((B2:B<>"")*row(B2:B)),"",row(B2:B)))
```

Explain:
- the `IF` statement compares the ordinal number of each rows with the last non-blank row
- if it is superior, the output will be blank
- if it is inferior, the output is the ordinal number of that row 
